-- Solves Project Euler 12

def countGroups xs =
    if null xs then return []

    result := []
    last := head xs
    count := 1
    xs = tail xs

    while not $ null xs do
        if head xs == last then
            count += 1
        else
            result = count : result
            last = head xs
            count = 1

        xs = tail xs

    return reverse $ count : result

def factor n =
    if n == 1 then return []

    factors := []
    i := 2

    while n > 1 do
        while n mod i == 0 do
            factors = i : factors
            n /= i

        i += 1

    return reverse factors

def countDivisors x =
    if x == 1 then return 1

    primeCounts := countGroups $ factor x
    count := 1

    for n in primeCounts do
        count *= (n + 1)

    return count

def firstNumber m =
    n := 0
    i := 1
    while True do
        n += i

        if countDivisors n > m then
            return n

        i += 1

print $ show $ firstNumber 500
