-- Solves Project Euler 12

def countGroups xs :: List -> List =
    if null xs then return []

    let result = []
    let last = head xs
    let count = 1
    xs = tail xs

    while not $ null xs do
        if head xs == last then
            count += 1
        else
            result = count : result
            last = head xs
            count = 1

        xs = tail xs

    return reverse $ count : result

def factor n :: Int -> List =
    if n == 1 then return []

    let factors = []
    let i = 2

    while n > 1 do
        while n mod i == 0 do
            factors = i : factors
            n /= i

        i += 1

    return reverse factors

def countDivisors x :: Int -> Int =
    if x == 1 then return 1

    let primeCounts = countGroups $ factor x
    let count = 1

    for n in primeCounts do
        count *= (n + 1)

    return count

def firstNumber m :: Int -> Int =
    let n = 0
    let i = 1
    while True do
        n += i

        if countDivisors n > m then
            return n

        i += 1

print $ firstNumber 500
