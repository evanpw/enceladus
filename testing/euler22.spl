# Solves Project Euler #22

import List
import String

def readNames() -> [String]
    line := readLine()

    pieces := line.unwrap().splitBy(',')

    result := []
    for piece in pieces
        result = piece.trimChar('"') :: result

    return result

def score(name: String) -> Int
    total := 0
    for i = 0 to name.length() - 1
        c := name.at(i)
        total += (c - 'A') + 1

    return total

names := readNames()
names = sort(cmpString, names)

total := 0
for i = 1 to names.length()
    name := names.at(i - 1)

    total += i * score(name)

print $ show(total)
